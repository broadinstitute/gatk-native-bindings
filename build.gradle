//Note: this section 'buildscript` is only for the dependencies of the buildscript itself.
buildscript {
    repositories {
        mavenCentral()
     }
}

plugins {
    id "java-library"
    id 'maven-publish'
    id 'signing'
    id 'com.palantir.git-version' version '0.11.0' //version helper
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-proc:none', '-Xlint:all','-Werror','-Xdiags:verbose']
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('Xdoclint:none', '-quiet')
}


final isRelease = Boolean.getBoolean("release")
version = (isRelease ? gitVersion() : gitVersion() + "-SNAPSHOT").replaceAll(".dirty", "")

logger.info("build for version:" + version)

group = 'org.broadinstitute'

wrapper {
    gradleVersion = '8.12.1'
}

publishing {
    publications {
        gatkNativeBindings(MavenPublication) {
            from components.java

            pom {
                name = 'gatk-native-bindings'
                packaging = 'jar'
                description = 'Bindings for native libraries to implement to be compatible with GATK4'
                url = 'http://github.com/broadinstitute/gatk-native-bindings'


                scm {
                    url = 'scm:git@github.com:broadinstitute/gatk-native-bindings.git'
                    connection = 'scm:git@github.com:broadinstitute/gatk-native-bindings.git'
                    developerConnection = 'scm:git@github.com:broadinstitute/gatk-native-bindings.git'
                }

                developers {
                    developer {
                        id = "gatkdev"
                        name = "GATK Development Team"
                        email = "gatk-dev-public@broadinstitute.org"
                    }
                }

                licenses {
                    license {
                        name = 'BSD 3-Clause'
                        url = 'https://github.com/broadinstitute/gatk-native-bindings/blob/master/LICENSE.TXT'
                        distribution = 'repo'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = isRelease ? project.findProperty("sonatypeUsername") : System.env.ARTIFACTORY_USERNAME
                password = isRelease ? project.findProperty("sonatypePassword") : System.env.ARTIFACTORY_PASSWORD
            }
            def release = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshot = "https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/"
            url = isRelease ? release : snapshot
        }
    }
}

/**
 * Sign non-snapshot releases with our secret key.  This should never need to be invoked directly.
 */
signing {
    required = { isRelease && gradle.taskGraph.hasTask("publishGatkNativeBindingsPublicationToMavenRepository") }
    sign publishing.publications.gatkNativeBindings
}

tasks.register('install') { dependsOn publishToMavenLocal }


