//Note: this section 'buildscript` is only for the dependencies of the buildscript itself.
buildscript {
    repositories {
        mavenCentral()
     }
}

plugins {
    id "java"
    id 'maven'
    id 'signing'
}

compileJava {
  options.compilerArgs = ['-proc:none', '-Xlint:all','-Werror','-Xdiags:verbose']
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "0.0.2"
logger.info("build for version:" + version)

group = 'org.broadinstitute'

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// This is a hack to disable the java 8 default javadoc lint until we fix the html formatting
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}
/**
 *This specifies what artifacts will be built and uploaded when performing a maven upload.
 */
artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

/**
 * Sign non-snapshot releases with our secret key.  This should never need to be invoked directly.
 */
signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

/**
 * Upload a release to sonatype.  You must be an authorized uploader and have your sonatype
 * username and password information in your gradle properties file. 
 *
 * For releasing to your local maven repo, use gradle install
 */
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.findProperty("sonatypeUsername"), password: project.findProperty("sonatypePassword"))
            }

            pom.project {
                name 'gatk-native-bindings'
                packaging 'jar'
                description 'Bindings for native libraries to implement to be compatible with GATK4'
                url 'http://github.com/broadinstitute/gatk-native-bindings'


                scm {
                    url 'scm:git@github.com:broadinstitute/gatk-native-bindings.git'
                    connection 'scm:git@github.com:broadinstitute/gatk-native-bindings.git'
                    developerConnection 'scm:git@github.com:broadinstitute/gatk-native-bindings.git'
                }

                developers {
                    developer {
                        id = "gatkdev"
                        name = "GATK Development Team"
                        email = "gatk-dev-public@broadinstitute.org"
                    }
                }

                licenses {
                    license {
                        name 'BSD 3-Clause'
                        url 'https://github.com/broadinstitute/gatk-native-bindings/blob/master/LICENSE.TXT'
                        distribution 'repo'
                    }
                }
            }
        }
    }
    doFirst{
        System.out.println("Uploading version $version")
    }
}
